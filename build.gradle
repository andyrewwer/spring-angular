import java.time.Duration

buildscript {
	ext {
		springBootVersion = '1.5.18.RELEASE'
		flywayGradlePluginVersion = '3.2.1'
		dockerComposePluginVersion = '0.6.14'
		dockerPluginVersion = '1.2'

	}
	repositories {
		maven { url "https://plugins.gradle.org/m2/" }
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
		classpath("org.flywaydb:flyway-gradle-plugin:${flywayGradlePluginVersion}")
		classpath("com.avast.gradle:gradle-docker-compose-plugin:${dockerComposePluginVersion}")
		classpath("se.transmode.gradle:gradle-docker:${dockerPluginVersion}")

	}
}

plugins {
	id "com.moowork.node" version "1.2.0"
}



apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.moowork.node'
apply plugin: 'org.flywaydb.flyway'
apply plugin: 'docker-compose'
apply plugin: 'docker'
group = 'com.deus'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

node {
	version = '10.9.0'
	distBaseUrl = 'https://nodejs.org/dist'
	download = true

	workDir = file('build/nodejs/')
	nodeModulesDir = file('frontend/')
}

ext.generatedSourceDir = file("frontend/dist")

sourceSets {
	main {
		resources {
			srcDirs += [generatedSourceDir]
		}
	}
}

dependencies {
	compile 'org.flywaydb:flyway-core:3.2.1'

	compile 'org.springframework.boot:spring-boot-starter-data-jpa'
	compile 'org.springframework.boot:spring-boot-starter-web'

	compile 'org.postgresql:postgresql'
	compileOnly 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dockerCompose {
	dockerComposeWorkingDirectory = 'docker/'
	useComposeFiles = ['docker-compose-test.yml']
	captureContainersOutput = true
	removeContainers = true
	waitForHealthyStateTimeout = Duration.ofSeconds(300)
}

task npmBuild(type: NpmTask,
		group: 'frontend',
		description: 'Builds the frontend assets and puts them in the main application resource directory',
		dependsOn: ['npmInstall', 'npmLint']) {
	args = ['run', 'build', '--silent']
}

task npmWatch(type: NpmTask,
		group: 'frontend',
		description: 'Build and watches the client side assets for rebuilding') {
	args = ['run', 'buildWatch']
}

task npmWatchIntellij(type: NpmTask,
		group: 'frontend',
		description: 'Build and watches the client side assets for rebuilding') {
	args = ['run', 'buildWatch', '--', '--op=../out/production/resources/static/']
}

task npmLint(type: NpmTask,
		group: 'frontend',
		description: 'Runs linter on the frontend sources.',
		dependsOn: ['npmInstall']) {
	args = ['run', 'lint']
}

task npmTest(type: NpmTask,
		group: 'frontend',
		description: 'Runs frontend unit tests.',
		dependsOn: ['npmInstall', 'npmLint']) {
	args = ['run', 'test', '--silent']
}

compileJava.dependsOn(processResources)
processResources.dependsOn(tasks.npmBuild)
test.dependsOn(['npmTest'])

task startDatabase(dependsOn: [composeUp])
task stopDatabase(dependsOn: [composeDown])
